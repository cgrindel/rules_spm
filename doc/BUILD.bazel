load(
    "@cgrindel_bazel_doc//bazeldoc:bazeldoc.bzl",
    "doc_for_provs",
    "write_file_list",
    "write_header",
    doc_providers = "providers",
)

# Lovingly inspired by
# https://github.com/bazelbuild/rules_swift/blob/021c11b1d578ffba547140eb24854cdfe74c794f/doc/BUILD.bazel#L3

# MARK: - Documentation Declarations

_API_SRCS = [
    "spm_common",
    "spm_package_info_utils",
    "spm_versions",
    "packages",
    "package_descriptions",
    "providers",
    "platforms",
    "references",
    "repository_utils",
]

_DOC_WITH_SYMBOLS = {
    "build_rules": [
        "spm_archive",
        "spm_clang_library",
        "spm_filegroup",
        "spm_package",
        "spm_swift_binary",
        "spm_swift_library",
        "spm_system_library",
    ],
    "providers": [
        "SPMPackageInfo",
        "SPMPackagesInfo",
        "SPMPlatformInfo",
        "SPMToolchainInfo",
    ],
    "workspace_rules": [
        "spm_repositories",
        "spm_pkg",
        "spm_rules_dependencies",
    ],
}

_API_DOC_PROVIDERS = [
    doc_providers.create(
        name = name,
        stardoc_input = "//spm:spm.bzl",
        symbols = [name],
        deps = ["//spm"],
    )
    for name in _API_SRCS
]

_ALL_DOC_PROVIDERS = [
    doc_providers.create(
        name = name + "_overview",
        stardoc_input = "//spm:spm.bzl",
        symbols = symbols,
        deps = ["//spm"],
    )
    for [
        name,
        symbols,
    ] in _DOC_WITH_SYMBOLS.items()
] + [
    doc_providers.create(
        name = "api",
        is_stardoc = False,
        stardoc_input = "//spm:spm.bzl",
        deps = ["//spm"],
    ),
] + _API_DOC_PROVIDERS

# MARK: - Special Case api.md

# Write the api.md_ file as a special case.
write_file_list(
    name = "api_doc",
    out = "api.md_",
    doc_provs = _API_DOC_PROVIDERS,
    header_content = [
        "# Build API",
        "",
        "The APIs described below are used by [the workspace rules](/doc/workspace_rules_overview.md) and",
        "[the build rules](/doc/build_rules_overview.md) to facilitate the build and exposition of the",
        "Swift packages.",
        "",
    ],
)

# MARK: - Headers

write_header(
    name = "providers_overview_header",
    header_content = [
        "# Providers",
        "",
        "The providers described below are used by [the build rules](/doc/build_rules_overview.md) to",
        "facilitate the build and exposition of the Swift packages.",
    ],
    symbols = _DOC_WITH_SYMBOLS["providers"],
)

write_header(
    name = "build_rules_overview_header",
    header_content = [
        "# Build Rules",
        "",
        "The rules described below are used to build Swift",
        "packages and make their outputs available as Bazel targets. Most",
        "clients will not use these rules directly. They are an implementation",
        "detail for [the workspace rules](/doc/workspace_rules_overview.md).",
    ],
    symbols = _DOC_WITH_SYMBOLS["build_rules"],
)

write_header(
    name = "workspace_rules_overview_header",
    header_content = [
        "# Workspace Rules",
        "",
        "The rules and functions described below are used in your WORKSPACE file to",
        "confgure `rules_spm` and to declare the Swift packages that are dependencies",
        "of your project.",
    ],
    symbols = _DOC_WITH_SYMBOLS["workspace_rules"],
)

# Write the API headers
[
    write_header(
        name = doc_prov.header_label,
        out = doc_prov.header_basename,
        header_content = [
            "# `{name}` API".format(name = doc_prov.name),
        ],
    )
    for doc_prov in _API_DOC_PROVIDERS
    if doc_prov.is_stardoc
]

doc_for_provs(doc_provs = _ALL_DOC_PROVIDERS)
